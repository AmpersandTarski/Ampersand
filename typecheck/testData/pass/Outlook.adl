CONTEXT Outlook
	{-
    SERVICE index : V
     = [ people : V
         = [ person : I[Person] -- go to 'accounts [Person]'
           , account : accountOwner~
             = [ name :  boxName -- will take you to 'accountMails [Account]'
               , address : boxMail;fullAddress -- same
               , emails : mailbox~
				 = [ from : sentBy;fullAddress -- only show the full address
				   , subject -- this is short for: subject subject
				   ]
               ]
           ]
       ]
    -}
	SERVICE Accounts [Person]
	 = [ account : accountOwner~ ]
	
	SERVICE AccountMails [Account]
	 = [ email : mailbox~
				 = [ from : sentBy;fullAddress
				   , subject
				   ]
	   , yourAddress : boxMail
	   , owner : accountOwner
	   ]
	
	SERVICE AllMails [Person]
	 = [ email : accountOwner~;mailbox~
				 = [ from : sentBy;fullAddress
				   , subject
				   ]
	   ]
	
	SERVICE AddressInfo [Address]
	 = [ fullAddress
	   , domain
	   , account : boxMail~
	   ]
	
	SERVICE ReadMail [Email]
	 = [ mailbox
	   , sentBy
	     = [fullAddress]
	   , subject
	   , messageText
	   ]
	
	PATTERN Emails
	  mailbox      :: Email   -> Account       PRAGMA "Email " " is in account " "".
	  messageText  :: Email   -> Blob          PRAGMA "Email " " reads out " "".
	  sentBy       :: Email   -> Address       PRAGMA "Email " " is sent by " "".
	  subject      :: Email   -> String        PRAGMA "Email " " is titled " "".
	  accountOwner :: Account -> Person        PRAGMA "Account " " is owned by " "".
	  boxName      :: Account -> String        PRAGMA "Account " " is called " "".
	  boxMail      :: Account -> Address [SUR] PRAGMA "Account " " uses email adress " "".
	  fullAddress  :: Address -> String        PRAGMA "The mail address " " reads out " "".
	  domain       :: Address -> String        PRAGMA "The domain name of mail address " " is " "".
	  
	ENDPATTERN
ENDCONTEXT