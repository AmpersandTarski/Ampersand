-- This model is used at 
-- https://ampersandtarski.gitbook.io/documentation/installing-ampersand/installing-the-tools-manually#test-to-see-if-you-can-build-your-first-prototype

CONTEXT HelloWorld IN ENGLISH
PURPOSE CONTEXT HelloWorld
{+This script allows you to check whether or not you can actually use Ampersand.+}

REPRESENT MyName TYPE ALPHANUMERIC
sessionMyName :: SESSION * MyName [UNI]
MEANING "My name can be known in the current session."

ROLE User MAINTAINS RegistrationRule
RULE RegistrationRule: "_SESSION"[SESSION] |- sessionMyName;sessionMyName~
VIOLATION (TXT "You can find the 'Registration' item in the navigationbar (top of the screen).")

INTERFACE Registration: "_SESSION"[SESSION] cRud BOX
   [ "My name is" : sessionMyName cRUd 
   ]

sayHelloReq :: SESSION * SESSION [PROP]
ROLE ExecEngine MAINTAINS Say_hello_when_name_is_specified
RULE Say_hello_when_name_is_specified: "_SESSION"[SESSION] /\ sessionMyName;sessionMyName~ |- sayHelloReq
VIOLATION (TXT "{EX} SetNavToOnCommit;/Hello_44__32_World"
          ,TXT "{EX} InsPair;sayHelloReq;SESSION;", SRC I, TXT ";SESSION;", TGT I
          )

INTERFACE HelloWorld: "_SESSION"[SESSION] cRud BOX
   [ "Hello, world. My name is" : sessionMyName cRud
   ]

ENDCONTEXT  