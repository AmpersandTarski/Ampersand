CONTEXT Ticket440 IN DUTCH

PATTERN VoldoenAanNormen
heeftVoldaanAan   :: Afdeling * Norm   MEANING "Een afdeling heeft voldaan aan een norm."
isDeelnormVan :: Norm * Norm [ASY,IRF] MEANING "Een norm kan deelnorm zijn van een andere norm."

ROLE Afdelingsmanager MAINTAINS VoldoenAanNormen1
RULE VoldoenAanNormen1: (heeftVoldaanAan!-isDeelnormVan)  |- heeftVoldaanAan
MEANING "Als aan alle deelnormen is voldaan, dan is ook aan de norm zelf voldaan"
VIOLATION (TXT "Afdeling '", SRC I, TXT "' voldoet nog niet aan norm '", TGT I, TXT "'.")

ROLE Afdelingsmanager MAINTAINS VoldoenAanNormen2
RULE VoldoenAanNormen2: -(-heeftVoldaanAan;isDeelnormVan)  |- heeftVoldaanAan
MEANING "Als er geen deelnorm is waar niet aan is voldaan, dan is ook aan de norm zelf voldaan"
VIOLATION (TXT "Afdeling '", SRC I, TXT "' heeft nog niet voldaan aan norm '", TGT I, TXT "'.")

RULE VoldoenAanNormen3: (heeftVoldaanAan!-isDeelnormVan) = -(-heeftVoldaanAan;isDeelnormVan)
MEANING "Of je het nu op de ene of de andere manier uitrekent zou niet uit moeten maken"
VIOLATION (TXT "Berekening voor Afdeling '", SRC I, TXT "' en norm '", TGT I, TXT "' zijn verschillend.")

ENDPATTERN
--------------------------------------------------------
POPULATION isDeelnormVan     CONTAINS [ ("N1", "N") ]
POPULATION isDeelnormVan     CONTAINS [ ("N2", "N") ]
POPULATION heeftVoldaanAan   CONTAINS [ ("Afd1", "N3") ]
--------------------------------------------------------
INTERFACE TEST : I[ONE]
BOX[ "Afdelingen" : V[ONE*Afdeling]
   BOX[ "Afdeling" : I
      , "Voldoet aan" : heeftVoldaanAan
      ]
   , "Normen" : V[ONE*Norm]
   BOX[ "Norm" : I
      , "Is deelnorm van" : isDeelnormVan
      , "Heeft deelnormen" : isDeelnormVan~
      ]
   ]
--------------------------------------------------------
ENDCONTEXT