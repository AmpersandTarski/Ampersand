CONTEXT Ticket440 IN DUTCH

PATTERN VoldoenAanNormen LABEL "Voldoen aan normen"
RELATION heeftVoldaanAan[Afdeling*Norm]   MEANING "Een afdeling heeft voldaan aan een norm."
RELATION isDeelnormVan[Norm*Norm] [ASY,IRF] MEANING "Een norm kan deelnorm zijn van een andere norm."

ROLE Afdelingsmanager MAINTAINS Normen1
RULE Normen1 LABEL "Voldoen aan normen [1]": (heeftVoldaanAan!-isDeelnormVan)  |- heeftVoldaanAan
MEANING "Als aan alle deelnormen is voldaan, dan is ook aan de norm zelf voldaan"
VIOLATION (TXT "Afdeling '", SRC I, TXT "' voldoet nog niet aan norm '", TGT I, TXT "'.")

ROLE Afdelingsmanager MAINTAINS Normen2
RULE Normen2 LABEL "Voldoen aan normen [2]": -(-heeftVoldaanAan;isDeelnormVan)  |- heeftVoldaanAan
MEANING "Als er geen deelnorm is waar niet aan is voldaan, dan is ook aan de norm zelf voldaan"
VIOLATION (TXT "Afdeling '", SRC I, TXT "' heeft nog niet voldaan aan norm '", TGT I, TXT "'.")

RULE Normen3 LABEL "Voldoen aan normen [3]": (heeftVoldaanAan!-isDeelnormVan) = -(-heeftVoldaanAan;isDeelnormVan)
MEANING "Of je het nu op de ene of de andere manier uitrekent zou niet uit moeten maken"
VIOLATION (TXT "Berekening voor Afdeling '", SRC I, TXT "' en norm '", TGT I, TXT "' zijn verschillend.")

ENDPATTERN
--------------------------------------------------------
POPULATION isDeelnormVan     CONTAINS [ ("N1", "N") ]
POPULATION isDeelnormVan     CONTAINS [ ("N2", "N") ]
POPULATION heeftVoldaanAan   CONTAINS [ ("Afd1", "N3") ]
--------------------------------------------------------
INTERFACE TEST : I[ONE]
BOX[ "Afdelingen" : V[ONE*Afdeling]
   BOX[ "Afdeling" : I
      , "Voldoet aan" : heeftVoldaanAan
      ]
   , "Normen" : V[ONE*Norm]
   BOX[ "Norm" : I
      , "Is deelnorm van" : isDeelnormVan
      , "Heeft deelnormen" : isDeelnormVan~
      ]
   ]
--------------------------------------------------------
ENDCONTEXT