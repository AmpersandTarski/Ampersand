CONTEXT FormalAmpersand IN ENGLISH 
INCLUDE "Concepts.adl"

PATTERN Pairs
    RELATION in[Pair*Relation] [UNI,TOT]
    RELATION lAtom[Pair*Atom] [UNI,TOT]
    RELATION rAtom[Pair*Atom] [UNI,TOT]
ENDPATTERN

PATTERN Atoms
    RULE IdentifyConcepts LABEL "Identifiability of concepts" : name;name~ |- I[Concept]
    VIOLATION (TXT "The name \"", SRC I, TXT "\" is already in use")

    RELATION in[Pair*Relation] [UNI,TOT] -- TOT is maintained by the exec-engine
    RELATION lAtom[Pair*Atom] [UNI,TOT,SUR]  -- SUR is maintained by the exec-engine
    RELATION rAtom[Pair*Atom] [UNI,TOT,SUR]  -- SUR is maintained by the exec-engine
ENDPATTERN

PATTERN ComputePopulations LABEL "Compute populations"
    RELATION pop[Atom*Concept]
    MEANING "If a pop c, we say that atom  a  is in the population of concept  c."

    ROLE ExecEngine MAINTAINS insPop, delPop
    RULE insPop : lAtom~;in[Pair*Relation];source[Relation*Concept];isaStar \/ rAtom~;in[Pair*Relation];target[Relation*Concept];isaStar |- pop[Atom*Concept]
    VIOLATION (TXT "{EX} InsPair;pop[Atom*Concept];Atom;", SRC I, TXT ";Concept;", TGT I)
    RULE delPop : pop[Atom*Concept] |- lAtom~;in[Pair*Relation];source[Relation*Concept];isaStar \/ rAtom~;in[Pair*Relation];target[Relation*Concept];isaStar
    VIOLATION (TXT "{EX} DelPair;pop[Atom*Concept];Atom;", SRC I, TXT ";Concept;", TGT I)

    ROLE ExecEngine MAINTAINS TOTin
    RULE TOTin : I[Pair] |- in[Pair*Relation];in[Pair*Relation]~
    VIOLATION ( TXT "{EX} DelPair;lAtom;Pair;", SRC I, TXT ";Atom;", TGT in[Pair*Relation]
              , TXT "{EX} DelPair;rAtom;Pair;", SRC I, TXT ";Atom;", TGT in[Pair*Relation]
              , TXT "{EX} DelAtom;Pair;", SRC I
              )

    ROLE ExecEngine MAINTAINS SURlAtomAndrAtom
    RULE SURlAtomAndrAtom LABEL "SUR lAtom and rAtom" : I[Atom] |- lAtom~;lAtom \/ rAtom~;rAtom
    VIOLATION ( TXT "{EX} DelAtom;Atom;", SRC I
              )
ENDPATTERN

ENDCONTEXT