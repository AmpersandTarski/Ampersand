CONTEXT AST IN ENGLISH LATEX
-- The commented version of Atoms can be found in Atoms.doc

PROCESS Atoms
RELATION repr[Atom*TEXT] [UNI,TOT]
MEANING "Every atom has a representation, which identifies that atom."

RELATION pop[Atom*Concept] [TOT] 
MEANING "If $a\\ \\id{pop}\\ c$, we say that atom $a$ is in the population of concept $c$."

RELATION name[Concept*TEXT] [UNI]
MEANING "Every concept has a name, which identifies that concept."

RULE InjName : name;name~ |- I[Concept]
MEANING "The name of a concept identifies that concept."
VIOLATION (TXT "The name \"", SRC I, TXT "\" is already in use")

RULE InjRepr : repr;repr~ /\ pop;pop~ |- I[Atom]
MEANING "Together, a concept and the representation identifies an atom uniquely."
VIOLATION (TXT "The name \"", SRC I, TXT "\" is already in use")

RELATION sub[Concept*Concept]
MEANING "$s\\ \\id{sub}\\ g$ means that each element of concept $s$ is an element of concept $g$ as well."

RULE "specialization" : pop;sub |- pop
MEANING "An atom that is an instance of a specialization of $c$ is an instance of $c$ as well."
VIOLATION (TXT "{EX} InsPair;pop;Atom;", SRC I, TXT ";Concept;", TGT I)
ROLE ExecEngine MAINTAINS specialization

RELATION name[Relation*TEXT] [UNI,TOT] 
RELATION source[Relation*Concept] [UNI,TOT] 
RELATION target[Relation*Concept] [UNI,TOT] 

RELATION in[Pair*Relation] [UNI,TOT]
RELATION l[Pair*Atom] [UNI,TOT]
RELATION r[Pair*Atom] [UNI,TOT]

RULE "source type" : l~;in;source |- pop
MEANING "Every atom at the left of a pair is in the population of the source concept of that relation."
RULE "target type" : r~;in;target |- pop
MEANING "Every atom at the right of a pair is in the population of the target concept of that relation."
VIOLATION (TXT "{EX} InsPair;pop;Atom;", SRC I, TXT ";Concept;", TGT I)
ROLE ExecEngine MAINTAINS "source type", "target type"
ENDPROCESS

{-
RELATION partOf[Concept*Typology] [UNI,TOT]
MEANING "Every concept is part of exactly one typology"

RULE "typologies" : sub;partOf |- partOf
MEANING "A specialisation of a concept and the concept itself are both part of the same typology."

RULE "Integrity of Atoms and populations": partOf~;pop~;pop;partOf |- I
MEANING "Atoms can be in the population of different Concepts only if such Concepts belong to the same Typology"
-}
INTERFACE Overview: '_SESSION'
TABS[ atoms : V[SESSION*Atom]
      COLS [ repr : repr
           , pop  : pop
           ]
    , concepts :V[SESSION*Concept]
      COLS [ name             : I
           , specializations  : sub~
           ]
    , pairs : V[SESSION*Pair]
      COLS [ pair           : I
           , "relation(s)"  : in
           ]
    , relations : V[SESSION*Relation]
      COLS [ name    : name
           , source  : source
           , target  : target
           ]
--    , garbage : V[SESSION*Atom];-((l~;in;source;pop~/\I) \/ (r~;in;target;pop~/\I))
--      COLS [ repr : repr
--           , pop  : pop
--           ]
    ]

VIEW Atom : Atom(repr,TXT ":",pop)

INTERFACE Atom(repr,pop) : I[Atom]
COLS [ repr : repr
     , pop  : pop
     ]

VIEW Concept : Concept(name)

INTERFACE concept(name[Concept*TEXT],sub) : I[Concept]
COLS [ name             : name
     , generalizations  : sub
     , specializations  : sub~
     ]

VIEW Pair : Pair(TXT "(",l;repr,TXT ",",r;repr,TXT ")")

INTERFACE Pair(l,r,in) : I[Pair]
COLS [ pair           : I
     , l              : l
     , r              : r
     , "relation(s)"  : in
     ]

VIEW Relation : Relation(name,TXT "[",source;name,TXT "]",target;name)

INTERFACE Relation(name[Relation*TEXT],source,target) : I[Relation]
COLS [ name    : name
     , source  : source
     , target  : target
     ]

{-
POPULATION sub CONTAINS [ ( "Asub_", "Agen_") ]
POPULATION sub CONTAINS [ ( "Bsub_", "Bgen_") ]

POPULATION name CONTAINS [ ( "Asub_", "Asub") ]
POPULATION name CONTAINS [ ( "Agen_", "Agen") ]
POPULATION name CONTAINS [ ( "Bsub_", "Bsub") ]
POPULATION name CONTAINS [ ( "Bgen_", "Bgen") ]

POPULATION partOf CONTAINS [ ( "Asub_", "Atypo_") ]
POPULATION partOf CONTAINS [ ( "Agen_", "Atypo_") ]
POPULATION partOf CONTAINS [ ( "Bsub_", "Btypo_") ]
POPULATION partOf CONTAINS [ ( "Bgen_", "Btypo_") ]

POPULATION pop CONTAINS [ ( "A1_", "Agen_" ) ]
POPULATION pop CONTAINS [ ( "B1_", "Bgen_" ) ]
POPULATION pop CONTAINS [ ( "A1sub_", "Asub_" ) ]
POPULATION pop CONTAINS [ ( "B1sub_", "Bsub_" ) ]
POPULATION pop CONTAINS [ ( "A1sub_", "Agen_" ) ]
POPULATION pop CONTAINS [ ( "B1sub_", "Bgen_" ) ]

POPULATION repr CONTAINS [ ( "A1_", "A1" ) ]
POPULATION repr CONTAINS [ ( "B1_", "B1" ) ]
POPULATION repr CONTAINS [ ( "A1sub_", "A1sub" ) ]
POPULATION repr CONTAINS [ ( "B1sub_", "B1sub" ) ]
-}

ENDCONTEXT