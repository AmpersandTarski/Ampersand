--! It is allowed to change texts and/or the order of texts IF AND ONLY IF this is also done in the corresponding Haskell files !--

-- SJ: I made some changes. By the way, the story is being told in the accompanying file called Interfaces.docadl.
CONTEXT RAP IN ENGLISH

INCLUDE "Concepts.adl"
--INCLUDE "AtomsAsShouldBe.adl"

PATTERN FormalAmpersand.StaticInterfaceStructure LABEL "Static Interface Structure"
    CLASSIFY FormalAmpersand.Interface ISA FormalAmpersand.ObjectDef
    CONCEPT FormalAmpersand.ObjectDef "An ObjectDef is the (recursive) structure that corresponds with a BOX in an Ampersand script." 

    CLASSIFY FormalAmpersand.ObjectDef ISA FormalAmpersand.Term
    CONCEPT FormalAmpersand.Term "Formerly known as Expression." 

    {-Han,
    ifcParams contain the editable relations. This needs to be enhanced to ifcInputs and ifcOutputs.
    So I have substituted ifcParams by ifcInputs everywhere in the Haskell code.
    ifcOutputs has to be added yet.
    -}
    RELATION FormalAmpersand.name[FormalAmpersand.Interface*FormalAmpersand.InterfaceName] [UNI,TOT]
    MEANING "This relation contains the name of each interface."
    REPRESENT FormalAmpersand.InterfaceName TYPE ALPHANUMERIC

    RELATION FormalAmpersand.context[FormalAmpersand.Interface*FormalAmpersand.Context][UNI]
    CONCEPT Interface "An interface is a mechanism that communicates data between different (two) contexts."

    REPRESENT FormalAmpersand.Origin TYPE ALPHANUMERIC
    RELATION FormalAmpersand.ifcPos[FormalAmpersand.Interface*FormalAmpersand.Origin] [UNI]
    MEANING "The position in the file (filename, line- and column number)."
    RELATION FormalAmpersand.ifcPurpose[FormalAmpersand.Interface*FormalAmpersand.Purpose] [UNI]
    MEANING "The purpose of each interface is being registered."

    RELATION FormalAmpersand.label[FormalAmpersand.FieldDef*FormalAmpersand.FieldName] [UNI,TOT]
    MEANING "Every field definition has a label."
    REPRESENT FormalAmpersand.FieldName TYPE ALPHANUMERIC
    RELATION FormalAmpersand.objpos[FormalAmpersand.ObjectDef*FormalAmpersand.Origin] [UNI]
    MEANING "Every object definition has a position in the text of the Ampersand source file (filename, line number and column number)."
    RELATION FormalAmpersand.objView[FormalAmpersand.ObjectDef*FormalAmpersand.View] [UNI]
    MEANING "An object definition can have a view that should be used for this object (e.g. TABS, TABLE, etc.)."
    RELATION FormalAmpersand.usedIn[FormalAmpersand.Relation*FormalAmpersand.Term]
    MEANING "If a relation is used in an term, this is registered in usedIn."
    RELATION FormalAmpersand.formalTerm[FormalAmpersand.Rule*FormalAmpersand.Term] 
    MEANING "The term, of which a rule consists, is accessible via the relation formalTerm."

    PURPOSE RELATION FormalAmpersand.edit[FormalAmpersand.FieldDef*FormalAmpersand.Relation]
    {+ To allow editing to insert, update and delete links,
    Ampersand registers whether the term in a field def is just the relation.+}
    RELATION FormalAmpersand.edit[FormalAmpersand.FieldDef*FormalAmpersand.Relation]
    MEANING "The term in this FieldDef is just the relation."
    PURPOSE RELATION FormalAmpersand.editFlp[FormalAmpersand.FieldDef*FormalAmpersand.Relation]
    {+ To allow editing to insert, update and delete links,
    Ampersand registers whether the term in a field def is just the flipped relation.+}
    RELATION FormalAmpersand.editFlp[FormalAmpersand.FieldDef*FormalAmpersand.Relation]
    MEANING "The term in this FieldDef is just the flipped relation."

ENDPATTERN

PATTERN FormalAmpersand.Interfaces
RELATION FormalAmpersand.ifcInputs[FormalAmpersand.Interface*FormalAmpersand.Relation]  -- was: ifcParams
MEANING "This relation contains the relations that can be edited in an interface by a user or another computer."
RELATION FormalAmpersand.ifcOutputs[FormalAmpersand.Interface*FormalAmpersand.Relation]
MEANING "This relation contains the relations that can be edited by the computer for restoring invariants."

RELATION FormalAmpersand.ifcRoles[FormalAmpersand.Interface*FormalAmpersand.Role]
MEANING "This relation contains the roles for which an interface is available (empty means: available for all roles)."
RELATION FormalAmpersand.ifcQuads[FormalAmpersand.Interface*FormalAmpersand.Quad] -- RJ: What is a Quad?
MEANING "All quads that are needed to perform computations for maintaining rules in an interface are registered."
RELATION FormalAmpersand.ifcConjuncts[FormalAmpersand.Interface*FormalAmpersand.Conjunct]
MEANING "All conjuncts that must be evaluated after a transation are registered."
RELATION FormalAmpersand.violatable[FormalAmpersand.Interface*FormalAmpersand.Rule]
MEANING "All rules that can be violated by an interface are included in violatable."

RULE FormalAmpersand.violatableByInterface : FormalAmpersand.violatable = (FormalAmpersand.ifcInputs\/FormalAmpersand.ifcOutputs);FormalAmpersand.usedIn;FormalAmpersand.formalTerm~
MEANING "An interface may violate any rule that uses an input- or output relation of that interface."
ENDPATTERN

ENDCONTEXT