CONTEXT FormalAmpersand IN ENGLISH 

PATTERN Concepts
    RELATION name[Concept*ConceptName] [TOT,SUR] --Not UNI because of aliases

    ROLE ExecEngine MAINTAINS "del unused ConceptName"
    RULE "del unused ConceptName" : I[ConceptName] |- name~;name
    MEANING "A ConceptName without Concept will be removed."
    VIOLATION ( TXT "{EX} DelAtom;ConceptName;", SRC I )
    
    IDENT Concept : Concept(name,context[Concept*Context])

    RELATION cddef2[ConceptDef*Meaning][UNI]
    -- RULE delUnusedMeaning : I[Meaning] |- cddef2~;cddef2
    -- MEANING "A Meaning without ConceptDef will be removed."  TODO: Fix this, because meanings can be connected to other stuff as well.
    -- VIOLATION ( TXT "{EX} DelAtom;ConceptDefinition;", SRC I )

    RELATION name[Relation*RelationName] [UNI,TOT]
    MEANING "Every relation has a name by which it can be referenced within its Context(s)."
    RELATION source[Relation*Concept] [UNI,TOT]
    RELATION target[Relation*Concept] [UNI,TOT]

    REPRESENT Identifier TYPE ALPHANUMERIC
    CLASSIFY ConceptName, RelationName ISA Identifier
    RELATION context[Concept*Context] [UNI] -- comes from concs, which is defined in FSpec. This contains all concepts declared inside a context but outside the patterns it contains.
ENDPATTERN

--    RULE "Identifiability of concepts": name[Concept*ConceptName];name[Concept*ConceptName]~ |- I[Concept]

PATTERN Specialization
    RELATION gengen[Isa*Concept] [UNI,TOT]
    MEANING "A generalization rule refers to one or more generic concepts."
    RELATION genspc[Isa*Concept] [UNI,TOT]
    MEANING "A generalization rule refers to one or more specific concepts."
    RELATION gengen[IsE*Concept] [TOT]
    MEANING "A generalization rule refers to one or more generic concepts."
    RELATION genspc[IsE*Concept] [UNI,TOT]
    MEANING "A generalization rule refers to one or more specific concepts."
ENDPATTERN

PATTERN "Compute Closures"
    RELATION isa[Concept*Concept] [ASY]
    MEANING "s isa g  means that each element of concept  s  is defined to be an element of concept  g  as well."
    RELATION isaStar[Concept*Concept] -- Transitive, reflexive closure of isa, aka isa*
    MEANING "s isaStar g  means that  s  is  g  or  each element of concept s  is an element of concept  g  as well."
    RELATION isaPlus[Concept*Concept] -- Transitive closure of isa, aka isa+
    MEANING "s isaPlus g  means that each element of concept  s  is an element of concept  g  as well."

    RELATION isaCopy[Concept*Concept] -- necessary only for calling the Transitive closure function.
    ROLE ExecEngine MAINTAINS "Compute transitive closure of isa"
    RULE "Compute transitive closure of isa" : isa[Concept*Concept] = isaCopy[Concept*Concept]
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} TransitiveClosure;isa[Concept*Concept];Concept;isaCopy[Concept*Concept];isaPlus")

    ROLE ExecEngine MAINTAINS "Compute transitive closure of isa by Ins", "Compute transitive closure of isa by Del"
    RULE "Compute transitive closure of isa by Ins" : isaPlus\/I |- isaStar
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} InsPair;isaStar;Concept;", SRC I, TXT ";Concept;", TGT I)
    RULE "Compute transitive closure of isa by Del" : isaStar |- isaPlus\/I
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} DelPair;isaStar;Concept;", SRC I, TXT ";Concept;", TGT I)
ENDPATTERN

PATTERN "Reflexive isa closures"
    RELATION isaRfxStar[Concept*Concept] -- Transitive, reflexive closure of isa[Concept*Concept]\/isa[Concept*Concept]~
    MEANING "s isaRfxStar g  means that  s  is  g  or  each element of concept s  is an element of concept  g  or vice-versa."
    RELATION isaRfxPlus[Concept*Concept] -- Transitive closure of isa[Concept*Concept]\/isa[Concept*Concept]~
    MEANING "s isaRfxPlus g  means that each element of concept  s  is an element of concept  g  or vice-versa."

    RELATION isaRfx[Concept*Concept] -- isaRfx[Concept*Concept]=isa[Concept*Concept]\/isa[Concept*Concept]~
    ROLE ExecEngine MAINTAINS "Compute isaRfxIns", "Compute isaRfxDel"
    RULE "Compute isaRfxIns" : isa[Concept*Concept]\/isa[Concept*Concept]~ |- isaRfx[Concept*Concept]
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} InsPair;isaRfx[Concept*Concept];Concept;", SRC I, TXT ";Concept;", TGT I)
    RULE "Compute isaRfxDel" : isaRfx[Concept*Concept] |- isa[Concept*Concept]\/isa[Concept*Concept]~
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} DelPair;isaRfx[Concept*Concept];Concept;", SRC I, TXT ";Concept;", TGT I)

    RELATION isaRfxCopy[Concept*Concept] -- necessary only for calling the Transitive closure function.
    ROLE ExecEngine MAINTAINS "Compute transitive closure of isa[Concept*Concept]\\/isa[Concept*Concept]~"
    RULE "Compute transitive closure of isa[Concept*Concept]\\/isa[Concept*Concept]~" : isaRfx[Concept*Concept] = isaRfxCopy[Concept*Concept]
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} TransitiveClosure;isaRfx[Concept*Concept];Concept;isaRfxCopy[Concept*Concept];isaRfxPlus[Concept*Concept]")

    ROLE ExecEngine MAINTAINS "Compute isaRfxStar by Ins", "Compute isaRfxStar by Del"
    RULE "Compute isaRfxStar by Ins" : isaRfxPlus[Concept*Concept]\/I |- isaRfxStar[Concept*Concept]
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} InsPair;isaRfxStar[Concept*Concept];Concept;", SRC I, TXT ";Concept;", TGT I)
    RULE "Compute isaRfxStar by Del" : isaRfxStar[Concept*Concept] |- isaRfxPlus[Concept*Concept]\/I
    MEANING "TODO: MEANING ONTBREEKT"
    VIOLATION (TXT "{EX} DelPair;isaRfxStar[Concept*Concept];Concept;", SRC I, TXT ";Concept;", TGT I)
ENDPATTERN

ENDCONTEXT