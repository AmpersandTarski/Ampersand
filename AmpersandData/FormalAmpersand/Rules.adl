CONTEXT AST IN ENGLISH LATEX
-- The comments for this script can be found in Rules.doc

PATTERN Rules
    VIEW Rule: Rule(name[Rule*RuleName])
    REPRESENT RuleName TYPE ALPHANUMERIC
    RELATION name[Rule*RuleName] [UNI,TOT]
    MEANING "The name of a rule."
      ROLE ExecEngine MAINTAINS "del unused RuleName"
      RULE "del unused RuleName" : I[RuleName] |- name~;name
      MEANING "A RuleName without Rule will be removed."
      VIOLATION ( TXT "{EX} DelAtom;RuleName;", SRC I )
    RELATION sign[Rule*Signature] [UNI,TOT]
    RELATION formalExpression[Rule*Expression] [UNI,TOT]
    MEANING "The rule expressed in relation algebra."
    RELATION propertyRule[Relation*PropertyRule] [SUR]
    MEANING "The properties of a relation."
    RELATION declaredthrough[PropertyRule*Property] [TOT]
    MEANING "A property is defined as part of the declaration of relation."
ENDPATTERN

PATTERN PropertyRules
    CONCEPT PropertyRule "A property rule is a rule, that is a property of a user-declared relation"
    CLASSIFY PropertyRule ISA Rule
    CONCEPT Property "UNI<|>TOT<|>INJ<|>SUR<|>RFX<|>IRF<|>SYM<|>ASY<|>TRN<|>PROP"
    RULE "property enum": I[Property] |- 'UNI' \/ 'TOT' \/ 'INJ' \/ 'SUR' \/ 'RFX' \/ 'IRF' \/ 'SYM' \/ 'ASY' \/ 'TRN' \/ 'PROP'
    MEANING "There are nine tokens, that can be used to define properties on a relation. -> is syntactic sugar for UNI and TOT; UNI means univalent; TOT means total; INJ means injective; SUR means surjective; RFX means reflexive; IRF means irreflexive; SYM means symmetric; ASY means antisymmetric; TRN means transitive; and PROP means symmetric and antisymmetric."
    REPRESENT Property TYPE ALPHANUMERIC
ENDPATTERN

ENDCONTEXT