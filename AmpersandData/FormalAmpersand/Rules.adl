CONTEXT AST IN ENGLISH LATEX
-- The documentation of this script can be found in Rules.docadl
-- This file has been aligned with ShowMeatgrinder on 2017-07-21 by SJ.

PATTERN Rules
    VIEW Rule: Rule(name[Rule*RuleName])
    REPRESENT RuleName TYPE ALPHANUMERIC
    IDENT Rules: Rule(allRules[Context*Rule]~,name[Rule*RuleName])

    RELATION ctxrs[Rule*Context] [UNI]  -- This contains all rules declared inside a context but outside the patterns it contains.
    MEANING "If a rule is declared in a context outside any pattern, that rule exists in that context. This is registered in the system."
    RELATION name[Rule*RuleName] [UNI,TOT] -- e.g. 'ASY bfOUprop::BusinessFunction*BusinessFunction'
    MEANING "The name of a rule."
      ROLE ExecEngine MAINTAINS "del unused RuleName"
      RULE "del unused RuleName" : I[RuleName] |- name~;name
      MEANING "A RuleName without Rule will be removed."
      VIOLATION ( TXT "{EX} DelAtom;RuleName;", SRC I )
    RELATION urlEncodedName[Rule*EncodedName] [UNI]
    MEANING "The location where a graphic of this rule can be found."
    RELATION origin[Rule*Origin] [UNI]
    MEANING "The location where a rule is defined."  -- e.g. 'line 367:10, file "C:\\\\Ampersand\\\\Workspace\\\\NRM\\\\NRM-BasicProcesses.adl"'
    RELATION message[Rule*Message] -- e.g. 'bfOUprop[BusinessFunction] is not antisymmetric'
--    RELATION srcConcept[Rule*Concept][UNI,TOT] -- e.g. 'BusinessFunction'
--    RELATION tgtConcept[Rule*Concept][UNI,TOT] -- e.g. 'BusinessFunction'
    RELATION formalTerm[Rule*Term] [UNI,TOT]
    MEANING "The rule expressed in relation algebra."
    RELATION meaning[Rule*Meaning] -- e.g. 'BusinessFunction' 
  --  RELATION sign[Rule*Signature] [UNI,TOT]
    RELATION declaredthrough[PropertyRule*Property] [TOT]
    MEANING "A property is defined as part of the declaration of relation."
    RELATION propertyRule[Relation*PropertyRule] [SUR]
    MEANING "The properties of a relation."
ENDPATTERN

PATTERN PropertyRules
    CONCEPT PropertyRule "A property rule is a rule, that is a property of a user-declared relation"
    CLASSIFY PropertyRule ISA Rule
    CONCEPT Property "UNI<|>TOT<|>INJ<|>SUR<|>RFX<|>IRF<|>SYM<|>ASY<|>TRN<|>PROP"
    RULE "property enum": I[Property] |- "UNI" \/ "TOT" \/ "INJ" \/ "SUR" \/ "RFX" \/ "IRF" \/ "SYM" \/ "ASY" \/ "TRN" \/ "PROP"
    MEANING "There are nine tokens, that can be used to define properties on a relation. -> is syntactic sugar for UNI and TOT; UNI means univalent; TOT means total; INJ means injective; SUR means surjective; RFX means reflexive; IRF means irreflexive; SYM means symmetric; ASY means antisymmetric; TRN means transitive; and PROP means symmetric and antisymmetric."
    REPRESENT Property TYPE ALPHANUMERIC
ENDPATTERN

ENDCONTEXT