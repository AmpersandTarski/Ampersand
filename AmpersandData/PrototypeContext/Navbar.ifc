CONTEXT PrototypeFramework IN ENGLISH

    VIEW "PF_NavMenuItem" : PF_NavMenuItem DEFAULT { label : label } ENDVIEW
    
    --[ADMIN INTERFACES to adapt the menus]--------------------------------------------------------
    INTERFACE "Edit navigation menu" FOR Administrator: "_SESSION";V[SESSION*PF_NavMenu] INTERFACE "Edit menu item"
    
    -- The following population is required because RELATION label[PF_Interface*PF_Label] must be total.
    -- That declaration is found in Interfaces.adl
    POPULATION label[PF_Interface*PF_Label] CONTAINS
     [ ("Edit_32_navigation_32_menu", "Edit_32_navigation_32_menu") ]

    INTERFACE "Edit menu item" FOR Administrator: I[PF_NavMenuItem] cRud BOX
        [ "Label"        : label
        , "Parent"       : isSubItemOf              cRUd
        , "Items"        : isSubItemOf~ CRuD BOX <TABLE sortable sortBy="Order" order="asc">
            [ "Item"     : I LINKTO INTERFACE "Edit menu item"
            , "Order"    : seqNr                    cRUd
            , "Visible"  : isVisible                cRUd
            , "Label"    : label                    cRUd
            , "Roles"    : pf_navItemRoles          cRud
            ]
        , "Interface ref": ifc                      cRUd
        , "Url"          : url                      cRUd
        , "Roles"        : pf_navItemRoles          cRUd
        ]

    --[SYSTEM INTERFACES to query menu items]------------------------------------------------------
    API "PF_MenuItems" FOR SYSTEM: sessionActiveRoles[SESSION*Role];pf_navItemRoles~;isVisible BOX
        [ "id"              : I
        , "label"           : label
        , "seqNr"           : seqNr
        , "ifc"             : ifc
        , "url"             : url
        , "parent"          : isSubItemOf
        ]
ENDCONTEXT