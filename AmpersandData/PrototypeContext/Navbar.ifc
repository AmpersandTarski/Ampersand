CONTEXT PrototypeFramework IN ENGLISH

    VIEW NavMenuItem : NavMenuItem DEFAULT { label : label } ENDVIEW
    
    --[ADMIN INTERFACES to adapt the menus]--------------------------------------------------------
    INTERFACE Editnavigationmenu LABEL "Edit navigation menu" FOR Administrator: "_SESSION";V[SESSION*NavMenu] INTERFACE Editmenuitem

    INTERFACE Editmenuitem LABEL "Edit menu item" FOR Administrator: I[NavMenuItem] cRud BOX
        [ "Label"        : label
        , "Parent"       : isSubItemOf              cRUd
        , "Items"        : isSubItemOf~ CRuD BOX <TABLE sortable sortBy="Order" order="asc">
            [ "Item"     : I LINKTO INTERFACE Editmenuitem
            , "Order"    : seqNr                    cRUd
            , "Visible"  : isVisible                cRUd
            , "Label"    : label                    cRUd
            , "Roles"    : navItemRoles          cRud
            ]
        , "Interface ref": ifc                      cRUd
        , "Url"          : url                      cRUd
        , "Roles"        : navItemRoles          cRUd
        ]

    --[SYSTEM INTERFACES to query menu items]------------------------------------------------------
    API MenuItems LABEL "Menu items" FOR SYSTEM: sessionActiveRoles[SESSION*Role];navItemRoles~;isVisible BOX
        [ "id"              : I
        , "label"           : label
        , "seqNr"           : seqNr
        , "ifc"             : ifc
        , "url"             : url
        , "parent"          : isSubItemOf
        ]
ENDCONTEXT