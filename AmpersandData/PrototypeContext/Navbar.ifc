CONTEXT PrototypeFramework IN ENGLISH

    VIEW "NavMenuItem" : NavMenuItem DEFAULT { label : label } ENDVIEW
    
    --[ADMIN INTERFACES to adapt the menus]--------------------------------------------------------
    INTERFACE "Edit navigation menu" FOR Administrator: "_SESSION";V[SESSION*NavMenu] INTERFACE "Edit menu item"
    
    INTERFACE "Edit menu item" FOR Administrator: I[NavMenuItem] cRud BOX
        [ "Label"        : label
        , "Parent"       : isSubItemOf              cRUd
        , "Items"        : isSubItemOf~ CRuD BOX <TABLE sortable>
            [ "Item"     : I LINKTO INTERFACE "Edit menu item"
            , "# "       : seqNr                    cRUd
            , "Visible"  : isVisible                cRUd
            , "Label"    : label                    cRUd
            , "Roles"    : navItemRoles          cRud
            ]
        , "Interface ref": ifc                      cRUd
        , "Url"          : url                      cRUd
        , "Roles"        : navItemRoles          cRUd
        ]

    --[SYSTEM INTERFACES to query menu items]------------------------------------------------------
    API "MenuItems" FOR SYSTEM: sessionActiveRoles[SESSION*Role];navItemRoles~;isVisible BOX
        [ "id"              : I
        , "label"           : label
        , "seqNr"           : seqNr
        , "ifc"             : ifc
        , "url"             : url
        , "parent"          : isSubItemOf
        ]

ENDCONTEXT