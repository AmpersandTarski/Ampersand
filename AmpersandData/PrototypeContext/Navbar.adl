CONTEXT PrototypeFramework IN ENGLISH

    --[NAV MENU ITEMS]-----------------------------------------------------------------------------
    CONCEPT NavMenuItem ""
        REPRESENT NavMenuItem TYPE OBJECT
  
    -- RELATION label[NavMenuItem*Label] needs to be TOT because the prototype framework depends on having an interface label
    -- The identitier of the NavMenuItem itself is a generated UUID, e.g. "_NavMenuItem_1550521084_00490187"
    -- We want to show the user a navigation label instead
    -- The navigation menu items and its label (and other attributes) are added by the prototype framework during application installation
    RELATION label[NavMenuItem*Label] [UNI,TOT] 
        MEANING "The human readable text that represents the navbar item and is shown to the user"
        CONCEPT Label ""
        REPRESENT Label TYPE ALPHANUMERIC

    RELATION isVisible[NavMenuItem*NavMenuItem] [PROP]
        MEANING "A navbar item can be visible or not"
    
    RELATION ifc[NavMenuItem*Interface] [UNI] 
        MEANING "A navbar item can direct to an interface"
        CONCEPT Interface "Representation of an Ampersand interface"

    RULE NoRefsToAPIs LABEL "Do not allow references to API interfaces" : ifc;isAPI |- -V
        MESSAGE "A navbar item can not reference an API interface"
  
    RELATION url[NavMenuItem*URL] [UNI]
        MEANING "A navbar item can direct to an (external) url. This can also be used for extensions like the excelimporter"
        CONCEPT URL "a uniform resource locator as defined by RFC3986"
        REPRESENT URL TYPE BIGALPHANUMERIC
  
    RELATION seqNr[NavMenuItem*SeqNr] [UNI]
        MEANING "A navbar item has a seq nr (default 0) which determines the position of the item"
        CONCEPT SeqNr ""
        REPRESENT SeqNr TYPE INTEGER
    
    RELATION isSubItemOf[NavMenuItem*NavMenuItem] [UNI,IRF,ASY]
        MEANING "A navbar item can be a sub item of another item"

    RELATION navItemRoles[NavMenuItem*Role]
        MEANING "A navbar item is accessible for a role"

    -- Add/remove menu items based on interface roles
    ROLE ExecEngine MAINTAINS AddNavItemRolesInterfaces
    RULE AddNavItemRolesInterfaces LABEL "Add navItemRoles for interfaces" : ifc;pf_ifcRoles |- pf_navItemRoles
    VIOLATION (TXT "{EX}InsPair;pf_navItemRoles;PF_NavMenuItem;", SRC I, TXT ";Role;", TGT I)

    ROLE ExecEngine MAINTAINS RemoveNavItemRolesInterfaces
    RULE RemoveNavItemRolesInterfaces LABEL "Remove navItemRoles for interfaces" : (I /\ ifc;ifc~);pf_navItemRoles |- ifc;pf_ifcRoles
    VIOLATION (TXT "{EX}DelPair;pf_navItemRoles;PF_NavMenuItem;", SRC I, TXT ";Role;", TGT I)

    -- Add/remove parent menu items if needed
    ROLE ExecEngine MAINTAINS AddNavItemRolesParentItems
    RULE AddNavItemRolesParentItems LABEL "Add navItemRoles for parent items" : isSubItemOf~;pf_navItemRoles |- pf_navItemRoles
    VIOLATION (TXT "{EX}InsPair;pf_navItemRoles;PF_NavMenuItem;", SRC I, TXT ";Role;", TGT I)

    ROLE ExecEngine MAINTAINS RemoveNavItemRolesParentItems
    RULE RemoveNavItemRolesParentItems LABEL "Remove navItemRoles for parent items" : (I /\ isSubItemOf~;isSubItemOf);pf_navItemRoles |- isSubItemOf~;pf_navItemRoles
    VIOLATION (TXT "{EX}DelPair;pf_navItemRoles;PF_NavMenuItem;", SRC I, TXT ";Role;", TGT I)
    
    --[NAV MENUS]----------------------------------------------------------------------------------
    CONCEPT NavMenu ""
        CLASSIFY NavMenu ISA NavMenuItem
        REPRESENT NavMenu TYPE OBJECT

    RELATION isPartOf[NavMenuItem*NavMenu] [UNI,TOT]
        MEANING "A navbar item is part of a certain navmenu"

    ROLE ExecEngine MAINTAINS FixTOTisPartOf
    RULE FixTOTisPartOf LABEL "Fix TOT for isPartOf[PF_NavMenuItem*PF_NavMenu]" : I[PF_NavMenuItem];isSubItemOf;isPartOf |- isPartOf
    VIOLATION (TXT "{EX}InsPair;isPartOf;PF_NavMenuItem;", SRC I, TXT ";PF_NavMenu;", TGT I)

    ROLE ExecEngine MAINTAINS DesignateRootPFnavMenItem
    RULE DesignateRootPFnavMenItem LABEL "Automatically designate root of PF_NavMenItem tree as PF_NavMenu" : I[PF_NavMenuItem] - isSubItemOf;isSubItemOf~ |- I[PF_NavMenu]
    VIOLATION (TXT "{EX} SetConcept;PF_NavMenuItem;PF_NavMenu;", SRC I[PF_NavMenuItem])

    ROLE ExecEngine MAINTAINS DeassignNonRootPFnavMenItems
    RULE DeassignNonRootPFnavMenItems LABEL "De-assign non-root PF_NavMenItems as PF_NavMenu" : I[PF_NavMenu] |- I[PF_NavMenuItem] - isSubItemOf;isSubItemOf~ 
    VIOLATION (TXT "{EX} ClearConcept;PF_NavMenu;", SRC I[PF_NavMenu])

ENDCONTEXT