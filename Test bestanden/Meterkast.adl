CONTEXT Meterkast
{- Dit is de ŽŽnversie van de Meterkast.
zo geven we straks PHPPLUG's aan:
  PHPPLUG GetPersonByNameAndAge {"ACTION=SELECT"} : I[Persoon]
ÊÊ= [ name {"PHPARG","PHPTYPE=String"} : naam
ÊÊÊÊ, age {"PHPARG","PHPTYPE=Int"} : leeftijd
ÊÊÊÊ, persoon {"PHPRETURN", "PHPTYPE=Array"} : I[Persoon]
ÊÊÊÊÊÊÊÊ= [ name : naam
ÊÊÊÊÊÊÊÊÊÊ, age Ê: leeftijd
ÊÊÊÊÊÊÊÊÊÊ, dateOfBirth : geboortedatum]
ÊÊÊÊ]
-}

SQLPLUG UserTbl : I[User]
 = [Id{"SQLTYPE=Id"} : I
   ,name{"SQLTYPE=Text"} : name
   ,password{"SQLTYPE=Password"} : password
   ,teacher{"SQLTYPE=Id"} : teacher
   ]
SQLPLUG RightTbl : I[Operation]
 = [Id{"SQLTYPE=Id"} : I
   ,name{"SQLTYPE=Text"} : name
   ]
SQLPLUG ActieTbl : I[Actie]
 = [Id{"SQLTYPE=Id"} : I
   ,object{"SQLTYPE=Id"} : object
   ,type{"SQLTYPE=Id"} : right
   ]
SQLPLUG BestandTbl : I[Bestand]
 = [Id{"SQLTYPE=Id"} : I
   ,owner{"SQLTYPE=Id"} : owner
   ,path{"SQLTYPE=Text"} : path
   ]

PATTERN Meterkast
    
    name      :: User->Text [INJ].
    name      :: Operation->Text [INJ].
    path      :: Bestand->Text [INJ].
    password  :: User->Password.
    owner     :: Bestand -> User PRAGMA "the owner of " " is ".
    teacher   :: User * User [UNI,ASY] PRAGMA "the teacher of " " is ".
    right     :: User * Operation PRAGMA "" " may exercise ".
    object    :: Actie -> Bestand PRAGMA "" " is being executed on ".
    -- user      :: Actie -> User PRAGMA "" " is being executed by user ".
    -- user=object;owner
    right     :: Actie -> Operation PRAGMA "" " is being executed by activating ".
    
    teacher;teacher = -V EXPLANATION "No pupil may teach, and no teacher may be tought."
    
ENDPATTERN

  
  SERVICE Users : I[ONE]
   = [ Users : V[ONE*User]
        = [ nr {"TEST"}     : I[User]
          , name    : name
          , pwd     : password
          ]
     ]

  SERVICE User : I[User]
   = [ naam    : name
     , pwd     : password
     ]

  SERVICE Userbeheer : I[User]
   = [ naam      : name
     , pwd       : password
     , teacher   : teacher
     , bestanden : owner~
     , studenten : teacher~
     , rechten   : right
     ]

  SERVICE Student : teacher;teacher~ /\ I
   = [ naam    : name
     , docent  : teacher
     , files   : owner~
     ]

-- SERVICE Docent SERVICES Docent EDITS I[User], I[Right]
  SERVICE Docent : teacher~;teacher /\ I
   = [ naam      : name
     , bestanden : owner~
     , studenten : teacher~
     ]
  
  SERVICE Bestand : I[Bestand]
   = [ eigenaar : owner
     , path     : path
     , acties   : object[Actie*Bestand]~
     ]

  SERVICE Actie : I[Actie]
   = [ file     : object
     , operatie : right
     ]
  
  SERVICE Operatie : I[Operation]
   = [ naam : name
     ]
  
ENDCONTEXT
